import QtQuick
import QtQuick.Layouts
import Quickshell.Services.Mpris
import qs.common
import qs.common.widgets
import qs.services
import "../"
import "../home"
import "../home/noon"


SidebarItemContainer {
    id: root

    readonly property bool playing: BeatsService.player?.playbackState === MprisPlaybackState.Playing
    readonly property bool displayingLyrics: activeLyrics.showContent
    readonly property QtObject colors: BeatsService?.colors ?? Colors

    Keys.onPressed: event => {
        const ctrl = event.modifiers & Qt.ControlModifier;
        const shift = event.modifiers & Qt.ShiftModifier;
        const player = BeatsService.player;

        if (ctrl && shift) {
            switch (event.key) {
            case Qt.Key_R:
                LyricsService.fetchLyrics(BeatsService.artist || "", BeatsService.title || "");
                break;
            case Qt.Key_Right:
                player?.canControl && player.next();
                break;
            case Qt.Key_Left:
                player?.canControl && player.previous();
                break;
            case Qt.Key_S:
                player && (player.shuffle = !player.shuffle);
                break;
            default:
                return;
            }
        } else if (ctrl && event.key === Qt.Key_R) {
            BeatsService.cycleRepeat();
        } else {
            switch (event.key) {
            case Qt.Key_Up:
                AudioService?.sink?.audio && (AudioService.sink.audio.volume = Math.min(1.0, AudioService.sink.audio.volume + 0.05));
                break;
            case Qt.Key_Down:
                AudioService?.sink?.audio && (AudioService.sink.audio.volume = Math.max(0.0, AudioService.sink.audio.volume - 0.05));
                break;
            case Qt.Key_Space:
                player?.togglePlaying();
                break;
            case Qt.Key_Right:
                player?.canSeek && player.length && (player.position = Math.min(player.length, player.position + 10));
                break;
            case Qt.Key_Left:
                player?.canSeek && player.length && (player.position = Math.max(0, player.position - 10));
                break;
            default:
                return;
            }
        }
        event.accepted = true;
    }

    RLayout {
        spacing: Padding.huge

        anchors.fill: parent
        anchors.margins: detached ? Padding.massive : Padding.large

        Item {
            id: content
            Layout.fillWidth: true
            Layout.fillHeight: true
            LiveLyrics {
                id: activeLyrics
            }
            StyledRectangularShadow {
                target: bigCover
                show: LyricsService.syncedLyrics.length === 0
            }
            StyledLoader {
                id: bigCover
                anchors.centerIn: parent
                anchors.verticalCenterOffset: -35
                width: root.playing ? 320 : 285
                height: width
                fade: true
                asynchronous: true
                active: !root.displayingLyrics
                visible: !root.displayingLyrics
                sourceComponent: MusicCoverArt {
                    anchors.fill: parent
                    clip: true
                    radius: Rounding.silly
                    enableBorders: false
                }
                Behavior on width {
                    Anim {}
                }
            }

            ColumnLayout {
                spacing: Padding.massive
                anchors.fill: parent
                anchors.margins: Padding.huge
                Spacer {}
                PlayerSelector {}
                MediaPlayerControls {
                    showCover: root.displayingLyrics
                }
            }
        }

        Item {
            visible: root.expanded
            Layout.maximumWidth: 340
            Layout.rightMargin: root.detached ? Padding.massive : 0
            Layout.fillWidth: true
            Layout.fillHeight: true

            TracksQueue {
                id: queue
                anchors.fill: parent
                colors: root.colors
            }

            StyledRectangularShadow {
                target: queue
            }
        }
    }
}
